buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group='com.github.spacen'

android {
    namespace 'com.github.martoreto.opuscodec'
    compileSdkVersion 33
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 33
        consumerProguardFiles 'proguard-rules.pro'
        externalNativeBuild {
            ndkBuild {
                // We don't build for arm64-v8a for now, because ASM optimizations do not compile
                // (at least for the default floating point implementation) and ARM64 supports
                // also armeabi-v7a binaries natively, so let's use those, which should be more
                // efficient.
                abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        ndkBuild {
            path "src/main/jni/Android.mk"
        }
    }
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation 'androidx.annotation:annotation:1.7.1'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.github.spacen'
                artifactId = 'opuscodec'

                artifact bundleReleaseAar
            }
        }
    }
}
